apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
		url = "https://maven.pkg.github.com/erpcya/Repository"
        credentials {
        	username = System.properties['deploy.user'] ?: System.getenv("GITHUB_DEPLOY_USER")
            password = System.properties['deploy.token'] ?: System.getenv("GITHUB_DEPLOY_TOKEN")
		}
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def libraryVersion = '1.1.3'
def entityType = 'WHH'

sourceSets {
    main {
         java {
            srcDirs = ['base']
         }
    }
}

dependencies {
    compileOnly fileTree(dir: 'dependences', include: ['*.jar'])
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version, 
                   "EntityType": entityType)
    }
}

//	Create release for project
task createRelease(type: Zip) {
   dependsOn build
   from 'build/libs/'
   from 'xml/'
   include '*'
   include '*/*'
   archiveName project.name + '.zip'
   destinationDir(file('build/release/'))
   doLast {
        file('build/release/')
        	.listFiles({file -> file.isFile()} as FileFilter).sort()
        	.each { File file ->
            ant.checksum file: file
        }
   	}
}

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/erpcya/Repository"
            credentials {
                username = System.properties['deploy.user'] ?: System.getenv("GITHUB_DEPLOY_USER")
                password = System.properties['deploy.token'] ?: System.getenv("GITHUB_DEPLOY_TOKEN")
            }
        }
    }
    publications {
		mavenJava(MavenPublication) {
        	groupId 'org.erpya.adempiere.tools'
            artifactId 'withholding'
            version libraryVersion
           	artifact jar
           	pom {
                name = 'ADempiere Withholding Engine'
                description = 'This library have all related to Withholding Engine'
                 url = 'https://www.erpya.com/'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
            }
		}
	}
}